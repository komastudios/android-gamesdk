plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 19
        versionCode 9
        // LINT.IfChange
        versionName '0.22'
        // LINT.ThenChange(../README.md)
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
    }
}

dependencies {
    implementation project(path: ':memoryadvice_common')
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.2'
    implementation 'org.tensorflow:tensorflow-lite:2.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'com.google.guava:guava:30.1.1-android'
    androidTestImplementation 'net.jimblackler.jsonschemafriend:core:0.10.5'
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    exclude "**/AndroidManifest.xml"
    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier 'javadoc'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'com.google.android.games'
                artifactId = 'memory-advice'
                version = android.defaultConfig.versionName
                artifact(sourceJar)
                artifact(javadocJar)
                pom {
                    name = 'The Android Memory Assistance API'
                    url = 'https://android.googlesource.com/platform/frameworks/opt/gamesdk/+/refs/heads/master/test/memoryadvice'
                    description = 'An experimental library to help applications avoid exceeding ' +
                            'safe limits of memory use on devices.'
                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution = 'repo'
                        }
                    }
                }
            }
        }
    }
}
